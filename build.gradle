import org.apache.tools.ant.filters.ReplaceTokens
import org.gradle.plugins.signing.Sign

plugins {
    alias(libs.plugins.java)
    alias(libs.plugins.quarkus)
    alias(libs.plugins.maven.publish)
    id 'signing'
    alias(libs.plugins.nmcp.single)
    alias(libs.plugins.axion.release)
    alias(libs.plugins.proto.toolchain)
    id 'idea'  // IntelliJ IDEA integration
}

// Axion-release configuration for automatic versioning
scmVersion {
    tag {
        prefix = 'v'
    }
    checks {
        uncommittedChanges = false
        aheadOfRemote = false
        snapshotDependencies = false
    }
}

// Proto toolchain configuration - uses git-proto-workspace mode for cross-module imports
pipestreamProtos {
    sourceMode = 'git-proto-workspace'
    gitRepo = "https://github.com/ai-pipestream/pipestream-protos.git"
    gitRef = "main"
    generateMutiny = true
    generateDescriptors = true

    modules {
        // Just register module names - workspace mode handles dependencies automatically
        register("common")
        register("connector-types")
        register("intake")
        register("admin")
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    // Import pipeline BOM to align all managed versions
    // Version defined in gradle.properties as single source of truth
    implementation platform("ai.pipestream:pipestream-bom:${pipestreamBomVersion}")
    
    implementation 'io.quarkus:quarkus-container-image-docker'

    // Dev services infrastructure (shared Docker Compose stack)
    implementation 'ai.pipestream:pipestream-quarkus-devservices'

    // Core Quarkus dependencies
    // Hibernate Reactive Panache for fully reactive database access
    implementation 'io.quarkus:quarkus-hibernate-reactive-panache'
    implementation 'io.quarkus:quarkus-reactive-pg-client'
    // JDBC driver still required for Flyway (Flyway doesn't support reactive drivers)
    implementation 'io.quarkus:quarkus-jdbc-postgresql'
    implementation 'io.quarkus:quarkus-rest'
    implementation 'io.quarkus:quarkus-smallrye-health'
    implementation 'io.quarkus:quarkus-arc'
    implementation 'io.quarkus:quarkus-flyway'
    implementation 'org.flywaydb:flyway-database-postgresql'
    implementation 'io.quarkus:quarkus-smallrye-openapi'
    implementation libs.quarkus.grpc
    implementation 'io.quarkus:quarkus-smallrye-stork'

    // Kafka dependencies
    implementation 'io.quarkus:quarkus-messaging-kafka'
    // Quarkus Apicurio Registry Protobuf Extension (Zero-Config)
    // Version managed by pipestream-bom
    implementation 'ai.pipestream:quarkus-apicurio-registry-protobuf'

    // Service Discovery dependencies
    implementation 'io.smallrye.stork:stork-service-discovery-consul'
    implementation 'io.smallrye.stork:stork-service-discovery-static-list'
    implementation 'io.smallrye.reactive:smallrye-mutiny-vertx-consul-client'
    implementation 'io.smallrye.stork:stork-service-registration-consul'

    // Password hashing
    implementation libs.password4j

    // WireMock server is used via Docker container in WireMockTestResource, not as a dependency
    // Pipestream libraries - versions managed entirely by the BOM/catalog
    // removed legacy stubs: implementation libs.pipestream.grpc.stubs
    implementation 'ai.pipestream:quarkus-dynamic-grpc'
    implementation 'ai.pipestream:pipestream-service-registration'

    // Test dependencies
    testImplementation platform("ai.pipestream:pipestream-bom:${pipestreamBomVersion}")
    testImplementation 'io.quarkus:quarkus-junit5'
    testImplementation 'io.quarkus:quarkus-test-hibernate-reactive-panache'
    testImplementation 'io.quarkus:quarkus-test-vertx'
    testImplementation libs.smallrye.reactive.messaging.in.memory
    testImplementation libs.awaitility
    testImplementation(testFixtures("ai.pipestream:pipestream-test-support:${pipestreamBomVersion}"))
    // Testcontainers - version managed by pipestream-bom
    testImplementation 'org.testcontainers:testcontainers'
    testImplementation 'org.testcontainers:testcontainers-junit-jupiter'
    integrationTestImplementation 'io.grpc:grpc-netty-shaded'
    integrationTestImplementation 'io.grpc:grpc-protobuf'
    integrationTestImplementation 'io.grpc:grpc-stub'
}


group 'ai.pipestream'
version "${scmVersion.version}"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withJavadocJar()
    withSourcesJar()
}

test {
    systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
}

compileTestJava {
    options.encoding = 'UTF-8'
}

// Configure Javadoc task
tasks.withType(Javadoc).configureEach { task ->
    // Use main sources only
    task.setSource(sourceSets.main.allJava)
    // Exclude any generated code if it leaks into sources
    task.exclude('**/v1/**')
    task.exclude('**/quarkus-generated-sources/**')

    // Javadoc options
    def opts = task.options
    opts.encoding = 'UTF-8'
    opts.charSet = 'UTF-8'
    opts.locale = 'en'
    // Be tolerant to doclint issues in dependent types
    if (opts.respondsTo('addStringOption')) {
        opts.addStringOption('Xdoclint:none', '-quiet')
    }
    // Do not fail the build on Javadoc issues
    task.failOnError = false
}

// Enable resource filtering to replace @version@ in application.properties
// Use @ delimiters to avoid conflicts with ${} runtime property placeholders
processResources {
    filesMatching('**/*.properties') {
        filter(ReplaceTokens, tokens: [
            version: project.version.toString()
        ])
    }
}


processTestResources {
    filesMatching('**/*.properties') {
        filter(ReplaceTokens, tokens: [
            version: project.version.toString()
        ])
    }
}

// Publishing configuration
publishing {
    publications {
        //noinspection GroovyAssignabilityCheck
        maven(MavenPublication) { publication ->
            from components.java
            //noinspection GroovyAssignabilityCheck
            artifact(file("${buildDir}/quarkus-app/quarkus-run.jar")) { artifact ->
                artifact.classifier = 'runner'
                artifact.builtBy tasks.named('quarkusBuild')
            }
            pom {
                name.set('Connector Admin Service')
                description.set('Administrative service for managing connectors in the Pipestream platform')
                url.set('https://github.com/ai-pipestream/connector-admin')

                licenses {
                    license {
                        name.set('MIT License')
                        url.set('https://opensource.org/licenses/MIT')
                    }
                }

                developers {
                    developer {
                        id.set('krickert')
                        name.set('Pipestream Platform Team')
                    }
                }

                scm {
                    connection.set('scm:git:git://github.com/ai-pipestream/connector-admin.git')
                    developerConnection.set('scm:git:ssh://github.com/ai-pipestream/connector-admin.git')
                    url.set('https://github.com/ai-pipestream/connector-admin')
                }
            }
        }
    }

    repositories {
        // Publish to Maven Local for development
        mavenLocal()

        // Publish to GitHub Packages when credentials are available (CI)
        def githubToken = providers.environmentVariable("GITHUB_TOKEN")
        if (githubToken.isPresent()) {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/ai-pipestream/connector-admin")
                credentials {
                    username = providers.environmentVariable("GITHUB_ACTOR").getOrElse("github-actions")
                    password = githubToken.get()
                }
                mavenContent {
                    releasesOnly()
                }
            }
        }
    }
}

signing {
    // Read org-level GPG secrets directly, so runnable apps don't need extra env mapping
    def signingKey = System.getenv("GPG_PRIVATE_KEY")
    def signingPassword = System.getenv("GPG_PASSPHRASE")

    if (signingKey && signingPassword) {
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications.maven
    }
}

// Ensure all signing tasks run before nmcp bundles and publishes artifacts
afterEvaluate {
    tasks.matching { it.name.startsWith("nmcpZipAllPublications") || it.name.startsWith("nmcpPublishAllPublicationsToCentralPortal") }
        .configureEach { nmcpTask ->
            nmcpTask.dependsOn(tasks.withType(Sign))
        }
}

nmcp {
    publishAllPublicationsToCentralPortal {
        username = providers.environmentVariable("MAVEN_CENTRAL_USERNAME")
        password = providers.environmentVariable("MAVEN_CENTRAL_PASSWORD")
        publishingType = "AUTOMATIC"
    }
}
