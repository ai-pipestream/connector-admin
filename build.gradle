plugins {
    alias(libs.plugins.java)
    alias(libs.plugins.quarkus)
    alias(libs.plugins.maven.publish)
    id 'signing'
    id 'com.gradleup.nmcp' version '1.2.0'
    id 'pl.allegro.tech.build.axion-release' version '1.18.16'
}

// Axion-release configuration for automatic versioning
scmVersion {
    tag {
        prefix = 'v'
        versionSeparator = ''
    }

    versionCreator 'versionWithBranch'

    snapshotCreator { version, position ->
        "${version.toString()}-SNAPSHOT"
    }

    hooks {
        pre 'fileUpdate', [
            files: ['gradle.properties'],
            pattern: { previousVersion, context -> /^version=.*$/ },
            replacement: { currentVersion, context -> "version=${currentVersion}" }
        ]
    }
}

def pipelineBomVersion = findProperty('pipelineBomVersion') ?: '0.1.2-SNAPSHOT'

dependencies {
    implementation 'io.quarkus:quarkus-container-image-docker'

    // Dev services infrastructure (shared Docker Compose stack)
    implementation libs.pipestream.compose.devservices
    
    // Use published BOM to align all versions
    implementation platform("ai.pipestream:pipeline-bom:${pipelineBomVersion}")

    // Core Quarkus dependencies
    implementation 'io.quarkus:quarkus-hibernate-orm-panache'
    implementation 'io.quarkus:quarkus-rest'
    implementation 'io.quarkus:quarkus-smallrye-health'
    implementation 'io.quarkus:quarkus-jdbc-mysql'
    implementation 'io.quarkus:quarkus-arc'
    implementation 'io.quarkus:quarkus-flyway'
    implementation 'org.flywaydb:flyway-mysql'
    implementation 'io.quarkus:quarkus-smallrye-openapi'
    implementation libs.quarkus.grpc
    implementation 'io.quarkus:quarkus-smallrye-stork'

    // Kafka dependencies
    implementation 'io.quarkus:quarkus-messaging-kafka'
    implementation libs.apicurio.registry.protobuf.serde

    // Service Discovery dependencies for Consul
    implementation 'io.smallrye.stork:stork-service-discovery-consul'
    implementation 'io.smallrye.reactive:smallrye-mutiny-vertx-consul-client'
    implementation 'io.smallrye.stork:stork-service-registration-consul'

    // Password hashing
    implementation libs.password4j

    // Pipeline libraries (published to Maven Local)
    testImplementation libs.pipestream.grpc.wiremock
    // Pipestream libraries - versions managed entirely by the BOM/catalog
    implementation libs.pipestream.grpc.stubs
    implementation libs.pipestream.commons
    implementation libs.pipestream.dynamic.grpc
    implementation libs.pipestream.dynamic.grpc.registration.clients

    // Test dependencies
    testImplementation 'io.quarkus:quarkus-junit5'
    testImplementation libs.smallrye.reactive.messaging.in.memory
    integrationTestImplementation 'io.grpc:grpc-netty-shaded'
    integrationTestImplementation 'io.grpc:grpc-protobuf'
    integrationTestImplementation 'io.grpc:grpc-stub'
}

group = 'ai.pipestream'
version = scmVersion.version

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

test {
    systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
}

compileTestJava {
    options.encoding = 'UTF-8'
}

// Publishing configuration
publishing {
    publications {
        //noinspection GroovyAssignabilityCheck
        maven(MavenPublication) { publication ->
            from components.java
            //noinspection GroovyAssignabilityCheck
            artifact(file("${buildDir}/quarkus-app/quarkus-run.jar")) { artifact ->
                artifact.classifier = 'runner'
                artifact.builtBy tasks.named('quarkusBuild')
            }
            pom {
                name.set('Connector Admin Service')
                description.set('Administrative service for managing connectors in the Pipestream platform')
                url.set('https://github.com/ai-pipestream/connector-admin')

                licenses {
                    license {
                        name.set('MIT License')
                        url.set('https://opensource.org/licenses/MIT')
                    }
                }

                developers {
                    developer {
                        id.set('krickert')
                        name.set('Pipestream Platform Team')
                    }
                }

                scm {
                    connection.set('scm:git:git://github.com/ai-pipestream/connector-admin.git')
                    developerConnection.set('scm:git:ssh://github.com/ai-pipestream/connector-admin.git')
                    url.set('https://github.com/ai-pipestream/connector-admin')
                }
            }
        }
    }

    repositories {
        // Publish to Maven Local for development
        mavenLocal()

        // Publish to GitHub Packages when credentials are available (CI)
        def githubToken = providers.environmentVariable("GITHUB_TOKEN")
        if (githubToken.isPresent()) {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/ai-pipestream/connector-admin")
                credentials {
                    username = providers.environmentVariable("GITHUB_ACTOR").getOrElse("github-actions")
                    password = githubToken.get()
                }
                mavenContent {
                    releasesOnly()
                }
            }
        }
    }
}

signing {
    def signingKey = System.getenv("GPG_SIGNING_KEY")
    def signingPassword = System.getenv("GPG_SIGNING_PASSPHRASE")

    if (signingKey && signingPassword) {
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications.maven
    }
}

nmcp {
    publishAllPublicationsToCentralPortal {
        username = providers.environmentVariable("MAVEN_CENTRAL_USERNAME")
        password = providers.environmentVariable("MAVEN_CENTRAL_PASSWORD")
        publishingType = "AUTOMATIC"
    }
}
